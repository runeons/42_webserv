//// lexer

ALPHA = 0x41-5A / 0x61-7A   ; A-Z / a-z

BIT = "0" / "1"

CHAR = 0x01-7F
                    ; any 7-bit US-ASCII character,
                    ; excluding NUL

CR = 0x0D
                    ; '\r'
                    ; carriage return

LF = 0x0A
                    ; '\n'
                    ; linefeed

CRLF = CR LF
                    ; Internet standard newline

CTL = 0x00-1F / 0x7F
                    ; controls

DIGIT = 0x30-39
                    ; 0-9

DQUOTE = 0x22
                    ; '"' (Double Quote)

HEX_SUFFIX = '0' ('x' / 'X')

HEXDIG = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

SP = 0x20
                    ; space

HTAB = 0x09
                    ; horizontal tab

WSP = SP / HTAB
                    ; white space

LWSP = *(WSP / CRLF WSP)
                    ; Use of this linear-white-space rule
                    ;  permits lines containing only white
                    ;  space that are no longer legal in
                    ;  mail headers and have caused
                    ;  interoperability problems in other
                    ;  contexts.
                    ; Do not use when defining mail
                    ;  headers and use with caution in
                    ;  other contexts.

OCTET = 0x00-FF
                    ; 8 bits of data

VCHAR = 0x21-7E
                    ; visible (printing) characters

DEC_NUMBER = *( DIGIT )

HEX_NUMBER = HEX_SUFFIX *( HEXDIG )

HEX_CHAR = HEX_SUFFIX HEXDIG HEXDIG

OWS = *( SP / HTAB )



//// parser

dec_octet   = DIGIT                 ; 0-9
			/ 0x31-39 DIGIT         ; 10-99   ; 1-9 0-9
			/ "1" 2DIGIT            ; 100-199 ; "1" 0-9 0-9
			/ "2" 0x30-34 DIGIT     ; 200-249 ; "2" 0-4 0-9
			/ "25" 0x30-35          ; 250-255 ; "25" 0-5

IPvFuture     = "v" 1*HEXDIG "." 1*( unreserved / sub_delims / ":" )

h16           = 1*4( HEXDIG )

ls32          = ( h16 ":" h16 ) / IPv4address

IPv6address =                            6( h16 ":" ) ls32
			/                       "::" 5( h16 ":" ) ls32
			/ [               h16 ] "::" 4( h16 ":" ) ls32
			/ [ *1( h16 ":" ) h16 ] "::" 3( h16 ":" ) ls32
			/ [ *2( h16 ":" ) h16 ] "::" 2( h16 ":" ) ls32
			/ [ *3( h16 ":" ) h16 ] "::"    h16 ":"   ls32
			/ [ *4( h16 ":" ) h16 ] "::"              ls32
			/ [ *5( h16 ":" ) h16 ] "::"              h16
			/ [ *6( h16 ":" ) h16 ] "::"

IPv4address = dec_octet "." dec_octet "." dec_octet "." dec_octet

segment_nz = 1*(pchar)

reg_name = *( unreserved / pct_encoded / sub_delims )

IP_literal = "[" ( IPv6address / IPvFuture  ) "]"

path_rootless = segment_nz *( "/" segment )

path_absolute = "/" [ segment_nz *( "/" segment ) ]

path_abempty = *( "/" segment )

sub_delims = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="

pct_encoded = "%" HEXDIG HEXDIG

unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~"

port = *DIGIT

host = IP_literal / IPv4address / reg_name

userinfo = *( unreserved / pct_encoded / sub_delims / ":" )

hier_part = "//" [ authority / path_abempty / path_absolute / path_rootless ]

scheme = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )

pchar = unreserved / pct_encoded / sub_delims / ":" / "@"

segment = *pchar

authority = [ userinfo "@" ] host [ ":" port ]

absolute_URI  = scheme ":" hier_part [ "?" query ]

query = *( pchar / "/" / "?" )

absolute_path = 1*( "/" segment )

tchar = "!" / "#" / "$" / "%" / "&" / "\'" / "*" / "+" / "-" / "." / "^" / "_" / "\`" / "|" / "~" / DIGIT / ALPHA

HTTP_name = 0x48.54.54.50 ; HTTP

asterisk_form = "*"

authority_form = authority

absolute_form = absolute_URI

origin_form = absolute_path [ "?" query ]

obs_fold = CRLF 1*( SP / HTAB )

obs_text = 0x80-FF

field_vchar = VCHAR / obs_text

field_content = field_vchar [ 1*( SP / HTAB ) field_vchar ]

token = 1*tchar

HTTP_version = HTTP_name "/" DIGIT "." DIGIT

request_target = origin_form / absolute_form / authority_form / asterisk_form

method = token

field_value = *( field_content / obs_fold )

field_name = token

request_line = method SP request_target SP HTTP_version CRLF

message_body = *OCTET

header_field = field_name ":" OWS field_value OWS

start_line = request_line

HTTP_message = start_line *( header_field CRLF ) CRLF [ message_body ]
